FUNCTION_BLOCK shower

VAR_INPUT
temp: REAL;
flow: REAL;
END_VAR

VAR_OUTPUT
cold: REAL;
hot: REAL;
END_VAR

FUZZIFY temp
RANGE := (-20.000 .. 20.000);
TERM cold := Trapezoid (-30, -30, -15, 0);
TERM good := Triangle (-10, 0, 10);
TERM hot := Trapezoid (0, 15, 30, 30);
END_FUZZIFY

FUZZIFY flow
RANGE := (-1.000 .. 1.000);
TERM soft := Trapezoid (-3, -3, -0.8, 0);
TERM good := Triangle (-0.4, 0, 0.4);
TERM hard := Trapezoid (0, 0.8, 3, 3);
END_FUZZIFY

DEFUZZIFY cold
RANGE := (-1.000 .. 1.000);
TERM closeFast := Triangle (-1, -0.6, -0.3);
TERM closeSlow := Triangle (-0.6, -0.3, 0);
TERM steady := Triangle (-0.3, 0, 0.3);
TERM openSlow := Triangle (0, 0.3, 0.6);
TERM openFast := Triangle (0.3, 0.6, 1);

METHOD : COG;
ACCU : MAX;
DEFAULT := nan | NC;
END_DEFUZZIFY

DEFUZZIFY hot
RANGE := (-1.000 .. 1.000);
TERM closeFast := Triangle (-1, -0.6, -0.3);
TERM closeSlow := Triangle (-0.6, -0.3, 0);
TERM steady := Triangle (-0.3, 0, 0.3);
TERM openSlow := Triangle (0, 0.3, 0.6);
TERM openFast := Triangle (0.3, 0.6, 1);

METHOD : COG;
ACCU : MAX;
DEFAULT := nan | NC;
END_DEFUZZIFY

RULEBLOCK 
AND : MIN;
OR : MAX;
ACT : MIN;

RULE 1 : if temp is cold and flow is soft then cold is openSlow and hot is openFast
RULE 2 : if temp is cold and flow is good then cold is closeSlow and hot is openSlow
RULE 3 : if temp is cold and flow is hard then cold is closeFast and hot is closeSlow
RULE 4 : if temp is good and flow is soft then cold is openSlow and hot is openSlow
RULE 5 : if temp is good and flow is good then cold is steady and hot is steady
RULE 6 : if temp is good and flow is hard then cold is closeSlow and hot is closeSlow
RULE 7 : if temp is hot and flow is soft then cold is openFast and hot is openSlow
RULE 8 : if temp is hot and flow is good then cold is openSlow and hot is closeSlow
RULE 9 : if temp is hot and flow is hard then cold is closeSlow and hot is closeFast
END_RULEBLOCK

END_FUNCTION_BLOCK
